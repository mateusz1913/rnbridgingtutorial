#### `ConicGradientViewComponentView.h`

Let's go to the header file for new architecture Fabric component at `ios/ConicGradientViewComponentView.h`

```objc title="ios/ConicGradientViewComponentView.h"
#if RCT_NEW_ARCH_ENABLED
#import <React/RCTViewComponentView.h>
#import <QuartzCore/QuartzCore.h>

/**
 * Declare the ObjC interface for that Fabric component class.
 * 
 * It must extend RCTViewComponentView
 */
@interface ConicGradientViewComponentView : RCTViewComponentView

@property(nonatomic, readonly, strong) CAGradientLayer * _Nonnull layer;

@end

#endif
```

We declare the class that extends `RCTViewComponentView` base class.
You can notice, that `layer` property is declared - it's imported from `QuartzCore` framework and will be responsible for displaying our gradient background.

#### `ConicGradientViewComponentView.mm`

Navigate to `ios/ConicGradientViewComponentView.mm` where we'll implement our Fabric component.

```objc title="ios/ConicGradientViewComponentView.mm"
#if RCT_NEW_ARCH_ENABLED
#import "ConicGradientViewComponentView.h"

#import <React/RCTConversions.h>
#import <RCTTypeSafety/RCTConvertHelpers.h>

#import <react/renderer/components/ConicGradientPackage/ComponentDescriptors.h>
#import <react/renderer/components/ConicGradientPackage/EventEmitters.h>
#import <react/renderer/components/ConicGradientPackage/Props.h>
#import <react/renderer/components/ConicGradientPackage/RCTComponentViewHelpers.h>

#import "RCTFabricComponentsPlugins.h"

using namespace facebook::react;

@interface ConicGradientViewComponentView () <RCTConicGradientViewViewProtocol>
@end

@implementation ConicGradientViewComponentView

@dynamic layer;

+ (Class)layerClass
{
    return [CAGradientLayer classForCoder];
}

- (instancetype)initWithFrame:(CGRect)frame
{
    if (self = [super initWithFrame:frame]) {
        static const auto defaultProps = std::make_shared<const ConicGradientViewProps>();
        _props = defaultProps;
        
        self.layer.type = kCAGradientLayerConic;
        self.layer.needsDisplayOnBoundsChange = YES;
    }

    return self;
}

+ (ComponentDescriptorProvider)componentDescriptorProvider
{
    return concreteComponentDescriptorProvider<ConicGradientViewComponentDescriptor>();
}

@end

Class<RCTComponentViewProtocol> ConicGradientViewCls(void)
{
    return ConicGradientViewComponentView.class;
}
#endif
```

Here we start with a bunch of imports for conversion helpers and code-generated spec classes.
Next up we import `facebook::react` C++ namespace to not write it every time before C++ classes which belong to that namespace.
After that, we implement code-generated `RCTConicGradientViewViewProtocol` protocol.

Inside the implementation block, you can find `@dynamic layer;` line. This will make the `layer` property be able to be implemented at runtime and not at compile time.
And this will be done at runtime with `layerClass` getter that returns the class type for our layer (`CAGradientLayer` in that case).
For more reference about the gradient class, check out [dedicated docs section](https://developer.apple.com/documentation/quartzcore/cagradientlayer?language=objc).

In the class initializer we are declaring default props used by the component as well as gradient-layer's properties.

Each Fabric component needs to declare static `componentDescriptorProvider` and `RCTComponentViewProtocol` class type (here it's `ConicGradientViewCls`).

When our Fabric component has its boilerplate, we can start with adding support for gradient props - let's add `updateProps:oldProps:` method:

```diff title="ios/ConicGradientViewComponentView.mm"
//

@implementation ConicGradientViewComponentView

//

// highlight-start
+- (void)updateProps:(Props::Shared const &)props oldProps:(Props::Shared const &)oldProps
+{
+    const auto &oldViewProps = *std::static_pointer_cast<const ConicGradientViewProps>(_props);
+    const auto &newViewProps = *std::static_pointer_cast<const ConicGradientViewProps>(props);
+
+    if (oldViewProps.colors != newViewProps.colors) {
+        auto colors = RCTConvertVecToArray(newViewProps.colors, ^id(SharedColor item){
+            return (id)RCTUIColorFromSharedColor(item).CGColor;
+        });
+        self.layer.colors = colors;
+    }
+
+    if (oldViewProps.locations != newViewProps.locations) {
+        auto locations = RCTConvertVecToArray(newViewProps.locations, ^id(double item){
+            return @(item);
+        });
+        self.layer.locations = locations;
+    }
+
+    if (oldViewProps.centerPoint.x != newViewProps.centerPoint.x || oldViewProps.centerPoint.y != newViewProps.centerPoint.y) {
+        auto centerPoint = CGPointMake(newViewProps.centerPoint.x, newViewProps.centerPoint.y);
+        self.layer.startPoint = centerPoint;
+        self.layer.endPoint = CGPointMake(1, centerPoint.y);
+    }
+
+    [super updateProps:props oldProps:oldProps];
+}
// highlight-end

//

@end

//
```

The newly added method is responsible for comparing new and old props and updating our component's gradient layer if needed.
For first two props, we are using `RCTConvertVecToArray` helper to map C++ array to ObjC array.
Additionally, C++ SharedColor type is mapped here to ObjC `UIColor` thanks to `RCTUIColorFromSharedColor` helper.
