import CompleteFile from '../_complete-file.mdx';

#### `SaveFilePickerModule.h`

Now, let's move to the module that will manage function calls from the JS world:

```objc title="ios/SaveFilePicker.h"
#import <React/RCTBridgeModule.h>

/**
 * When using Swift classes in ObjC header, the class must have its
 * "forward declaration"
 * 
 * @see https://developer.apple.com/documentation/swift/importing-swift-into-objective-c#Include-Swift-Classes-in-Objective-C-Headers-Using-Forward-Declarations
 */
@class SaveFilePickerModuleImpl;


/**
 * Declare the ObjC interface for that native module class.
 * 
 * It must extend NSObject (like every class in ObjC) and
 * implement RCTBridgeModule (like each RN native module).
 * 
 * If the module emits events, it must extend RCTEventEmitter class.
 */
@interface SaveFilePickerModule : NSObject<RCTBridgeModule>

@end
```

In the header file for `SaveFilePickerModule` class we declare our class extending `NSObject` (as every class in ObjC) and implementing `RCTBridgeModule` (as each RN iOS module class).

Additionally, to use Swift `SaveFilePickerModuleImpl` class, we must do "forward-declaration" (check out Apple's Swift-ObjC interop [dedicated docs section](https://developer.apple.com/documentation/swift/importing-swift-into-objective-c#Include-Swift-Classes-in-Objective-C-Headers-Using-Forward-Declarations)).

#### `SaveFilePickerModule.mm`

```objc title="ios/SaveFilePickerModule.mm"
#import "SaveFilePickerModule.h"

/**
 * When using Swift classes in ObjC implementation, the classes must be imported
 * from generated Objective-C Interface Header
 *
 * @see https://developer.apple.com/documentation/swift/importing-swift-into-objective-c#Import-Code-Within-an-App-Target
 */
#import "SaveFilePickerPackage-Swift.h"

#if RCT_NEW_ARCH_ENABLED
/**
 * Import header file with codegenerated protocols based on the JS specification
 *
 * The name of the header matches the name provided in codegenConfig's `name` field in package.json
 */
#import "SaveFilePickerPackage.h"

// Each turbo module extends codegenerated spec class
@interface SaveFilePickerModule () <NativeSaveFilePickerModuleSpec>
@end
#endif

// Declare the ObjC implementation for that native module class
@implementation SaveFilePickerModule

// Return the name of the module - it should match the name provided in JS specification
RCT_EXPORT_MODULE(SaveFilePickerModule)

#if RCT_NEW_ARCH_ENABLED
// Implement RCTTurboModule protocol
- (std::shared_ptr<facebook::react::TurboModule>)getTurboModule:
    (const facebook::react::ObjCTurboModule::InitParams &)params
{
    return std::make_shared<facebook::react::NativeSaveFilePickerModuleSpecJSI>(params);
}
#endif

@end
```

This is the implementation file for `SaveFilePickerModule` class.

Before declaring the implementation block, we have to import Swift-ObjC generated interface header (check out [dedicated docs section](https://developer.apple.com/documentation/swift/importing-swift-into-objective-c#Include-Swift-Classes-in-Objective-C-Headers-Using-Forward-Declarations)),
and we have to implement code-generated spec protocol for new architecture (code between `#if RCT_NEW_ARCH_ENABLED` & `#endif` directives).

After that, inside implementation block we have `RCT_EXPORT_MODULE` macro invoked with `SaveFilePickerModule` argument. This basically registers the module with provided name on iOS side, so that it's accessible in the JS world.

Also, for new architecture, it implements `- getTurboModule:` method (from `RCTTurboModule` protocol).
If you won't implement that method, XCode will complain that the code-generated spec protocol methods are not implemented.

Let's combine module-implementation with module class

```objc title="ios/SaveFilePickerModule.mm"
//

// highlight-start
@interface SaveFilePickerModule () <SaveFilePickerModuleDelegate>
@end

// Declare the ObjC implementation for that native module class
@implementation SaveFilePickerModule {
    SaveFilePickerModuleImpl *moduleImpl;
    RCTResponseSenderBlock callbackBlock;
    RCTPromiseResolveBlock resolveBlock;
    RCTPromiseRejectBlock rejectBlock;
}
// highlight-end

// Return the name of the module - it should match the name provided in JS specification
RCT_EXPORT_MODULE(SaveFilePickerModule)

// highlight-start
- (instancetype)init {
    self = [super init];
    if (self) {
        moduleImpl = [SaveFilePickerModuleImpl new];
        moduleImpl.delegate = self;
    }
    return self;
}

// Declare if module should be initialized on the main queue
+ (BOOL)requiresMainQueueSetup
{
    return YES;
}

/**
 * If the module interacts with UIKit,
 * it can declare that its methods should be run on main queue
 */
- (dispatch_queue_t)methodQueue {
    return dispatch_get_main_queue();
}
// highlight-end

//

@end
```

First, we declare private variable on `SaveFilePickerModule` for module-implementation which will be initialized in the `- init` method.
We can use `SaveFilePickerModuleImpl` Swift class, because it's imported from `SaveFilePickerPackage-Swift.h`.

We also declared private variables for callback and promise blocks, we will use them to communicate result back to the JS code.

Next, we override static method `+ requiresMainQueueSetup`, which must be done whenever `- init` method on the RN's wrapper class is overriden. 

As we will interact with the UIKit API, we will need to do it on the main queue.
We could just wrap UIKit code in `dispatch_async(dispatch_get_main_queue(), ^{ ... });`,
but there's also an option to specify the `- methodQueue` getter.
It's used to describe on which queue this module calls should be run.
So in our case, we will want to use the main queue - returning `dispatch_get_main_queue()` from the getter will do the job.

:::tip

To learn more about dispatch queues in ObjC, you may visit Apple's [dedicated guide](https://developer.apple.com/library/archive/documentation/General/Conceptual/ConcurrencyProgrammingGuide/OperationQueues/OperationQueues.html).

:::

We still have 2 things to do - as you may noticed, XCode displayed a warning about `SaveFilePickerModuleDelegate`.
The module class is marked to be implementing the delegate protocol,
the module-implementation's delegate property is set to this module instance,
but we still need to implement delegate's methods.

```objc title="ios/SaveFilePickerModule.mm"
//

@implementation SaveFilePickerModule {
    SaveFilePickerModuleImpl *moduleImpl;
    RCTResponseSenderBlock callbackBlock;
    RCTPromiseResolveBlock resolveBlock;
    RCTPromiseRejectBlock rejectBlock;
}

//

/**
 * If the module interacts with UIKit,
 * it can declare that its methods should be run on main queue
 */
- (dispatch_queue_t)methodQueue {
    return dispatch_get_main_queue();
}

// highlight-start
- (void)onSuccess
{
    if (callbackBlock != nil) {
        callbackBlock(@[@{ @"success": @(YES), @"cancelled": @(NO) }]);
    } else if (resolveBlock != nil) {
        resolveBlock(@(YES));
    }
    callbackBlock = nil;
    resolveBlock = nil;
    rejectBlock = nil;
}

- (void)onCancel
{
    if (callbackBlock != nil) {
        callbackBlock(@[@{ @"success": @(NO), @"cancelled": @(YES) }]);
    } else if (resolveBlock != nil) {
        resolveBlock(@(NO));
    }
    callbackBlock = nil;
    resolveBlock = nil;
    rejectBlock = nil;
}

- (void)onError:(NSError *)error
{
    if (callbackBlock != nil) {
        callbackBlock(@[@{ @"success": @(NO), @"cancelled": @(NO), @"error": @{ @"code": @(error.code), @"message": error.localizedDescription } }]);
    } else if (rejectBlock != nil) {
        rejectBlock([NSString stringWithFormat:@"%@", @(error.code)], error.localizedDescription, error);
    }
    callbackBlock = nil;
    resolveBlock = nil;
    rejectBlock = nil;
}
// highlight-end

@end
```

In the delegate's methods, we are invoking callback/promise response blocks, with the result arguments.
As a cleanup, we are setting response blocks to `nil`, so that these are not called second time.

The last thing is to export the methods to the JS layer.

```objc title="ios/SaveFilePickerModule.mm"
//

@implementation SaveFilePickerModule {
    SaveFilePickerModuleImpl *moduleImpl;
    RCTResponseSenderBlock callbackBlock;
    RCTPromiseResolveBlock resolveBlock;
    RCTPromiseRejectBlock rejectBlock;
}

//

/**
 * If the module interacts with UIKit,
 * it can declare that its methods should be run on main queue
 */
- (dispatch_queue_t)methodQueue {
    return dispatch_get_main_queue();
}

// highlight-start
// Exported methods are overriden - based on the spec class
RCT_EXPORT_METHOD(saveFileWithCallback : (NSString *)filename
                              callback : (RCTResponseSenderBlock)callback)
{
    callbackBlock = callback;
    [moduleImpl saveFileWithFilename:filename];
}

RCT_EXPORT_METHOD(saveFileWithPromise : (NSString *)filename
                              resolve : (RCTPromiseResolveBlock)resolve
                               reject : (RCTPromiseRejectBlock)reject)
{
    resolveBlock = resolve;
    rejectBlock = reject;
    [moduleImpl saveFileWithFilename:filename];
}
// highlight-end

- (void)onSuccess
{
    if (callbackBlock != nil) {
        callbackBlock(@[@{ @"success": @(YES), @"cancelled": @(NO) }]);
    } else if (resolveBlock != nil) {
        resolveBlock(@(YES));
    }
    callbackBlock = nil;
    resolveBlock = nil;
    rejectBlock = nil;
}

- (void)onCancel
{
    if (callbackBlock != nil) {
        callbackBlock(@[@{ @"success": @(NO), @"cancelled": @(YES) }]);
    } else if (resolveBlock != nil) {
        resolveBlock(@(NO));
    }
    callbackBlock = nil;
    resolveBlock = nil;
    rejectBlock = nil;
}

- (void)onError:(NSError *)error
{
    if (callbackBlock != nil) {
        callbackBlock(@[@{ @"success": @(NO), @"cancelled": @(NO), @"error": @{ @"code": @(error.code), @"message": error.localizedDescription } }]);
    } else if (rejectBlock != nil) {
        rejectBlock([NSString stringWithFormat:@"%@", @(error.code)], error.localizedDescription, error);
    }
    callbackBlock = nil;
    resolveBlock = nil;
    rejectBlock = nil;
}

@end
```

Inside exported methods, we are doing 2 things:
- we save callback/promise response blocks
- we use module-implementation to show the picker

<CompleteFile
  filename="SaveFilePickerModule.mm"
  language="objc"
>


    #import "SaveFilePickerModule.h"

    /**
     * When using Swift classes in ObjC implementation, the classes must be imported
     * from generated Objective-C Interface Header
     *
     * @see https://developer.apple.com/documentation/swift/importing-swift-into-objective-c#Import-Code-Within-an-App-Target
     */
    #import "SaveFilePickerPackage-Swift.h"

    #if RCT_NEW_ARCH_ENABLED
    /**
     * Import header file with codegenerated protocols based on the JS specification
     *
     * The name of the header matches the name provided in codegenConfig's `name` field in package.json
     */
    #import "SaveFilePickerPackage.h"

    // Each turbo module extends codegenerated spec class
    @interface SaveFilePickerModule () <NativeSaveFilePickerModuleSpec>
    @end
    #endif

    @interface SaveFilePickerModule () <SaveFilePickerModuleDelegate>
    @end

    // Declare the ObjC implementation for that native module class
    @implementation SaveFilePickerModule {
        SaveFilePickerModuleImpl *moduleImpl;
        RCTResponseSenderBlock callbackBlock;
        RCTPromiseResolveBlock resolveBlock;
        RCTPromiseRejectBlock rejectBlock;
    }

    // Return the name of the module - it should match the name provided in JS specification
    RCT_EXPORT_MODULE(SaveFilePickerModule)

    - (instancetype)init {
        self = [super init];
        if (self) {
            moduleImpl = [SaveFilePickerModuleImpl new];
            moduleImpl.delegate = self;
        }
        return self;
    }

    // Declare if module should be initialized on the main queue
    + (BOOL)requiresMainQueueSetup
    {
        return YES;
    }

    /**
    * If the module interacts with UIKit,
    * it can declare that its methods should be run on main queue
    */
    - (dispatch_queue_t)methodQueue {
        return dispatch_get_main_queue();
    }

    // Exported methods are overriden - based on the spec class
    RCT_EXPORT_METHOD(saveFileWithCallback : (NSString *)filename
                                  callback : (RCTResponseSenderBlock)callback)
    {
        callbackBlock = callback;
        [moduleImpl saveFileWithFilename:filename];
    }

    RCT_EXPORT_METHOD(saveFileWithPromise : (NSString *)filename
                                  resolve : (RCTPromiseResolveBlock)resolve
                                  reject : (RCTPromiseRejectBlock)reject)
    {
        resolveBlock = resolve;
        rejectBlock = reject;
        [moduleImpl saveFileWithFilename:filename];
    }

    - (void)onSuccess
    {
        if (callbackBlock != nil) {
            callbackBlock(@[@{ @"success": @(YES), @"cancelled": @(NO) }]);
        } else if (resolveBlock != nil) {
            resolveBlock(@(YES));
        }
        callbackBlock = nil;
        resolveBlock = nil;
        rejectBlock = nil;
    }

    - (void)onCancel
    {
        if (callbackBlock != nil) {
            callbackBlock(@[@{ @"success": @(NO), @"cancelled": @(YES) }]);
        } else if (resolveBlock != nil) {
            resolveBlock(@(NO));
        }
        callbackBlock = nil;
        resolveBlock = nil;
        rejectBlock = nil;
    }

    - (void)onError:(NSError *)error
    {
        if (callbackBlock != nil) {
            callbackBlock(@[@{ @"success": @(NO), @"cancelled": @(NO), @"error": @{ @"code": @(error.code), @"message": error.localizedDescription } }]);
        } else if (rejectBlock != nil) {
            rejectBlock([NSString stringWithFormat:@"%@", @(error.code)], error.localizedDescription, error);
        }
        callbackBlock = nil;
        resolveBlock = nil;
        rejectBlock = nil;
    }

    #if RCT_NEW_ARCH_ENABLED
    // Implement RCTTurboModule protocol
    - (std::shared_ptr<facebook::react::TurboModule>)getTurboModule:
        (const facebook::react::ObjCTurboModule::InitParams &)params
    {
        return std::make_shared<facebook::react::NativeSaveFilePickerModuleSpecJSI>(params);
    }
    #endif

    @end
</CompleteFile>
